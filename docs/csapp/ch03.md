## 3.2 程序编码

【例】Linux可以用命令行将源文件编译为可执行程序

```shell
gcc -Og -o p p1.c p2.c
```

- 源代码转换为可执行代码的过程
  - 源代码（p1.c和p2.c）：通过**编译器**生成汇编代码
  - 汇编代码（p1.s和p2.s）：通过**汇编器**转化成二进制目标代码文件
  - 目标代码（p1.o和p2.o）：通过**链接器**将目标代码合并为可执行文件p

【注】`-Og`代表原始C代码整体结构的机器代码的优化等级。如果要提高程序性能，可以使用较高的优化等级`-O1`或`-O2`，但是会破坏C代码的结构



### 3.2.2 代码示例

```cpp
long mult2(long, long);

void multstore(long x, long y, long *dest) {
    long t = mult2(x, y);
    *dest = t;
}
```

【编译】`gcc -Og -S mstore.c`，生成AT&T格式的汇编代码`mstore.s`。去掉伪指令后如下：

```cpp
// void multstore(long x, long y, long *dest)
// x in %rdi, y in %rsi, dest in %rdx
multstore:
        pushq   %rbx            // Save %rbx
        movq    %rdx, %rbx      // Copy dest to %rbx
        call    mult2           // Call mult2(x, y)
        movq    %rax, (%rbx)    // mult2的返回值是%rax，赋值到*dest中
        popq    %rbx            // Restore %rbx
        ret                     // return
```

【汇编】`gcc -Og -c mstore.c`，生成`mstore.o`。内部有一段代码对应目标代码

```
53 48 89 d3 e8 00 00 00 00 48 89 03 5b c3
```

【反汇编】`objdump -d mstore.o`。可以看到与编译的结果是类似的。区别：省略了一些指令的后缀'q'，call和ret加上了后缀'q'（其实省略掉也没问题）

```cpp
0000000000000000 <multstore>:
   0:   53                      push   %rbx
   1:   48 89 d3                mov    %rdx,%rbx
   4:   e8 00 00 00 00          callq  9 <multstore+0x9>
   9:   48 89 03                mov    %rax,(%rbx)
   c:   5b                      pop    %rbx
   d:   c3                      retq
```


## 3.3 数据格式

汇编代码的指令处理，某些指令是带后缀的，如`movb`、`movq`。

- **整型**：b代表字节（8bit）；w代表字（16bit）；l代表双字（32bit）；q代表四字（64bit）
- **浮点型**：有不同的指令，s代表单精度（4字节）；l代表双精度（8字节）

<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/8fe3c7e37e924cc29a8d20303df39229~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221128080753144.png"  /></p>


## 3.4 访问信息

通用寄存器：x86-64的CPU包含16个存储64位值的通用目的寄存器
- 寄存器的低位部分可以独立使用，兼容历史指令
- 不同的寄存器扮演不同的角色，比如`%rax`默认作为返回值

<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/62b3340a73d24c0790385754715b63c0~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221128081419954.png"  /></p>


### 3.4.1 操作数

AT&T汇编指令格式：**操作码 + 操作数**

```cpp
movq (%rdi), %rax
addq $8,     %rbx
```

操作数分为三种类型：

- **立即数**：表示常数。格式是`$数字`，比如`$-577`、`$0x1F`
- **寄存器**：表示某个寄存器的值，格式是`%寄存器`，比如`%rax`
- **内存引用**：相当于指针，根据地址访问某个内存的位置
  - 表示方式：$Imm(r_b, r_i, s)$，分为4个部分：立即数偏移$Imm$、基址寄存器$r_b$、变址寄存器$r_i$、比例因子$s$（必须是1、2、4、8之一）
  - 有效地址：$Imm+R[r_b]+R[r_i]*s$


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/36ee0f162f654e2d83df067aa5bcf3c5~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221128082359267.png"  /></p>



【例】书中例子：假设寄存器和内存地址的值如下


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/fc15c49b6e5b43149bfea69115100fbd~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221201081346924.png"  /></p>

那么所示操作数的值如下：


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/9ef749e100ea499e88e27e5ef88afad3~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221201081441398.png"  /></p>



### 3.4.2 数据传送指令——MOV
- `mov`：将数据从源位置复制到目的位置。根据操作数据大小，分为四种情况：`movb`、`movw`、`movl`、`movq`
```cpp
movl $0x4050, %eax        //4 bytes
movw %bp, %sp             //2 bytes
movb (%rdi, %rcx), %al    //1 byte
movb $-17, (%rsp)         //1 byte
movq %rax, -12(%rbp)      //8 bytes
```

【例】mov立即数
```cpp
movabsq $0x0011223344556677, %rax  //%rax = 0011223344556677
movb    $-1, %al                   //%rax = 00112233445566FF
movw    $-1, %al                   //%rax = 001122334455FFFF
movl    $-1, %al                   //%rax = 00000000FFFFFFFF
movq    $-1, %al                   //%rax = FFFFFFFFFFFFFFFF
```
【注】
1. 使用`movl`时，会把寄存器的高4位字节设置为0
2. 常规的`movq`只能以32位补码作为源立即数。64位立即数要用`movabsq`，且只能以寄存器作为目的位置



- **移动指令**：较小的源复制到较大的目的时使用
  - `movz`：零扩展，剩余位填充0（2.2.6节零扩展）。如：`movzbw`表示byte字节领扩展到word
  - `movs`：符号扩展，剩余位按符号扩展填充（2.2.6节符号扩展）
  - `cltq`：相当于`movslq %eax, %rax`
  - 注意：movzlq不存在，原因是与movl相同

<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/210b8f8f88184b64bec1e83ad88581cf~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221201081735001.png"  /></p>

【例】书中例子

```cpp
movabsq $0x0011223344556677, %rax  //%rax = 0011223344556677
movb    $0xAA, %dl                 //%dl = AA
movb    %dl, %al                   //%rax = 00112233445566AA
movsbq  %dl, %rax                  //%rax = FFFFFFFFFFFFFFAA
movzbq  %dl, %rax                  //%rax = 00000000000000AA
```



### 3.4.3 C语言的mov

```cpp
long exchange(long *xp, long y)
{
    long x = *xp;
    *xp = y;
    return x;
}
```
【编译】`gcc -Og -S exchange.c`
```cpp
// long exchange(long *xp, long y)
// xp in %rdi, y in %rsi
exchange:
    movq (%rdi), %rax
    movq %rsi, (%rdi)
    ret
```

**【说明】C语言的指针其实对应的就是取内存地址**



### 3.4.4 压入和弹出栈数据

- **栈**：`后进先出`原则的数据结构。可以看做一个数组，分两个方向：栈顶和栈底
  - 栈底：高地址
  - 栈顶：低地址，属于插入和删除数据的一端。`%rsp`保存栈顶元素的地址
- **栈向下增长，栈顶元素的地址一般是最低的**

<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/fb63fd57b2b64c0eadbea66b93246794~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221201211136186.png"  /></p>

【说明】

- 过程：
  - 假设栈已经有一些数据，栈顶是0x108
  - 此时执行pushq，则栈顶-8字节，寄存器值填入对应栈的内存
  - 此时执行popq，则栈顶+8字节，读取内存栈的内存到寄存器
- `pushq %rax`：将四字节数据压栈。等价于以下两条命令：

```cpp
subq $8, %rsp        //Decrement stack pointer
movq %rax, (%rsp)    //Store %rax on stack
```
- `popq %rdx`：栈弹出四字节数据。等价于以下两条命令：
```cpp
movq (%rsp), %rdx    //Read %rdx from stack
addq $8, %rsp        //Increment stack pointer
```

## 3.5 算数和逻辑操作


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/8810ce7fabfb4aad8f7869f10e1614a0~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221201215203576.png"  /></p>

### 3.5.1 加载有效地址leaq

`leaq`指令形式：`leaq (%rdi, %rsi, 4), %rax`

与`movq`区别：第一个指令看起来是内存形式，但是其实**没有读取内存，而是直接将有效地址写到目的操作数**

【理解】第一个指令是内存形式的话，相当于利用了$Imm+R[r_b]+R[r_i]*s$ 简化了指令运算

【例】书中例子，使用leaq简化函数的算数计算

```cpp
long scale(long x, long y, long z) {
    long t = x + 4 * y + 12 * z;
    return t;
}
```

【编译】

```cpp
//long scale(long x, long y, long z)
//x in %rdi, y in %rsi, z in %rdx
scale:
    leaq (%rdi,%rsi,4), %rax        //rax = x+4*y
    leaq (%rdx,%rdx,2), %rdx        //rdx = z+2*z = 3z
    leaq (%rax,%rdx,4), %rax        //rax = (x+4*y) + 4*(3z) = x+4*y+12*z
    ret
```



### 3.5.2 一元和二元操作
【例】书中例子
- `incq (%rsp)`：使得栈顶元素+1，即C语言的++运算
- `subq %rax, %rdx`：rdx-rax的结果赋值给rdx，即C语言的x-=y

【注】一元和二元操作的目的操作数不能为立即数（理解为立即数不能作为左值，寄存器和内存地址可以）



### 3.5.3 移位操作

- **左移**：算数左移（`SAL`）、逻辑左移（`SHL`），效果一样，都是右边填0
- **右移**：算数右移（`SAR`，填0）、逻辑右移（`SHR`，填符号）

【注】移位量可以是立即数，或单字节存放在寄存器`%cl`中

【例】书中例子，使用`leaq`和`salq`代替整数乘法。使用`mulq`太费时间

```cpp
leaq (%rdx,%rdx,2), %rax    //rax = rdx+rdx*2 = 3*rdx
salq $4, %rax               //rax = rax<<4 = 48*rdx
```



### 3.5.5 特殊的算数操作

- **乘法运算和除法运算**：由于结果溢出，存放到两个寄存器`rax`和`rdx`中：


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/a67b1a252e1f471d846f0bc39e2b7fc5~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221202062011773.png"  /></p>

【例】书中例子

```cpp
#include <inttypes.h>
typedef unsigned __int128 uint128_t;
void store_uprod(uint128_t *dest, uint64_t x, uint64_t y) {
    *dest = x * (uint128_t)y;
}
```

【编译】

```cpp
//void store_uprod(uint128_t *dest, uint64_t x, uint64_t y)
//dest in %rdi, x in %rsi, y in %rdx
store_uprod:
    movq %rsi, %rax
    mulq %rdx
    movq %rax, (%rdi)
    movq %rdx, 8(%rdi)
    ret
```



## 3.6 控制

### 3.6.1 条件码(Condition Code)

- CPU维护一组条件码寄存器，描述最近的算数或逻辑操作的属性，**用于条件分支指令**
- 图3-10中的整数运算，除了`leaq`外，都有可能设置条件码
    - 比如：移位操作，进位标志CF设置为最后一个被移出的位，溢出标志OF设置为0

| 标志 | 说明                                                         |
| ---- | ------------------------------------------------------------ |
| CF   | 进位标志。最近的操作最高位产生进位。用于检查无符号操作的溢出 |
| ZF   | 零标志。最近的操作得出结果0                                  |
| SF   | 符号标志。最近的操作的到的结果为负数                         |
| OF   | 溢出标志。最近的操作导致一个补码溢出（正溢出或负溢出）       |

- 还有两类指令`CMP`和`TEST`，**只改变条件码，而不改变其它寄存器**
  - `CMP`：与`SUB`行为一样，两个操作数相减，但不影响目的寄存器。如果两个数相等，ZF=1；如果不等，用SF、OF判断两数大小
  - `TEST`：与`AND`行为一样，但不影响目的寄存器。

【说明】操作数A < 操作数B，为什么等价于`SF^OF == 1`？

- 假设两个操作数的值分别是a和b，t=a-b
    - **case1**：a=10，b=20，则a<b，且t<0。未发生溢出，此时SF=1，OF=0，SF^OF=1
    - **case2**：a=20，b=10，则a>b，且t>0。未发生溢出，此时SF=0，OF=0，SF^OF=0
    - **case3**：a=-2，b=127，则a<b，且t=127>0。发生正溢出，此时SF=0，OF=1，SF^OF=1
    - **case4**：a=1，b=-128，则a>b，且t=-127<0。发生负溢出，此时SF=1，OF=1，SF^OF=0
    - 综上所述，`SF^OF==1`代表a<b。其它a<=b可表示为`(SF^OF)|ZF == 1` （图3-14）


### 3.6.2 访问条件码

- 使用`set`指令读取
  - `sete %rax`表示把ZF标志赋给rax
  - `setl %rax`表示把SF异或OF的结果赋值给rax

<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/4f07fb568a324ca589af743b3517b1d6~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221202065017920.png"  /></p>

【例】书中例子

```cpp
int compare(int a, int b) {
    return (a < b);
}
```

【编译】

```cpp
//int compare(int a, int b)
//a in %edi, b in %esi
compare:
        cmpl    %esi, %edi        //a-b的结果存放到条件码寄存器中
        setl    %al               //结果赋值给al寄存器
        movzbl  %al, %eax         //零扩展
        ret
```




### 3.6.3 跳转指令

- 分无条件跳转（`JMP`）和有条件跳转（如：`JE`、`JL`）
  - **无条件跳转**：操作数是标号，或者间接跳转到某个程序计数器PC处
  - **有条件跳转**：处理判断和`set`一样


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/2318d614d3e24cb5a23127371292933f~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221202072846864.png"  /></p>

【例】书中例子

```cpp
int absdiff(int x, int y) {
    long result;
    if (x < y) {
        result = y - x;
    } else {
        result = x - y;
    }
    return result;
}
```

【编译】`gcc -Og -S absdiff.c`

```cpp
//int absdiff(int x, int y)
//x in %edi, y in %esi
absdiff:
        cmpl    %esi, %edi        //Compare x:y
        jge     .L2               //If x>=y, goto .L2
        subl    %edi, %esi
        movslq  %esi, %rax        //result = y-x
        ret
.L2:
        subl    %esi, %edi
        movslq  %edi, %rax        //result = x-y
        ret
```



### 3.6.4 跳转指令的编码

- 跳转指令在通过汇编器、链接器后，将跳转目标的**相对地址转为绝对地址**

【例】书中例子branch.c

```cpp
long branch(long x) {
    long result = x;
    while (result > 0)
        result >>= 1;
    return result;
}

int main() {
    branch(1);
    return 0;
}
```

【编译】`gcc -Og -S branch.c`

```cpp
//long branch(long x)
//x in %rdi
branch:
        movq    %rdi, %rax
        jmp     .L2
.L3:
        sarq    %rax
.L2:
        testq   %rax, %rax
        jg      .L3
        rep ret
```

【反汇编】`gcc -Og -c branch.c; objdump -d branch.o`

```cpp
0000000000000000 <branch>:
   0:   48 89 f8                mov    %rdi,%rax
   3:   eb 03                   jmp    8 <branch+0x8>
   5:   48 d1 f8                sar    %rax
   8:   48 85 c0                test   %rax,%rax
   b:   7f f8                   jg     5 <branch+0x5>
   d:   f3 c3                   repz retq
```

【反汇编】`gcc -Og -o branch branch.c; objdump -d branch`

```cpp
00000000004004ed <branch>:
  4004ed:       48 89 f8                mov    %rdi,%rax
  4004f0:       eb 03                   jmp    4004f5 <branch+0x8>
  4004f2:       48 d1 f8                sar    %rax
  4004f5:       48 85 c0                test   %rax,%rax
  4004f8:       7f f8                   jg     4004f2 <branch+0x5>
  4004fa:       f3 c3                   repz retq
```

【注】可以看到经过链接器后，jmp的相对位置变为绝对位置，其余不变（这对理解汇编有好处）

【备注】`rep ret`：当ret指令通过跳转指令到达时，根据AMD的说法，处理器不能正确预测ret指令的目的。所以增加rep空操作，使得代码在AMD上运行更快。无视即可。

### 3.6.6 用条件传送来实现条件分支

【例】用条件传送来实现条件分支，提升性能

【编译】还是absdiff例子，`gcc -O2 -S absdiff.c`

```cpp
//int absdiff(int x, int y)
//x in %edi, y in %esi
absdiff:
        movl    %esi, %edx        //edx = y
        movl    %edi, %eax        //eax = x
        subl    %edi, %edx        //edx = y-x
        subl    %esi, %eax        //eax = x-y
        cmpl    %esi, %edi
        cmovl   %edx, %eax        //If x-y<0, eax=y-x
        ret
```

【说明】
1. 现代处理器采用了**分支预测逻辑**，**猜测跳转指令是否会执行**。如果猜测正确，会显著提高性能；猜测错误，则会招致很严重的惩罚。
2. 使用**条件控制转移语句**`cmovl`，可以提高代码的效率，这与现代处理器的**流水线处理**（pipelining）有关系。当然，并不是所有场景都适合于条件控制转移（书本148页）。条件传送指令有：

<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/db042211f5074c48bc91ea7a2d780819~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203163059663.png"  /></p>


### 3.6.7 循环

汇编语言中没有`while`、`for`，而是通过**条件测试和跳转指令实现循环**。
- *书中讲述了C代码先转换为goto版本，再转为汇编的过程，包括如何优化。感觉没必要抠得很深。知道是用跳转实现的循环即可。*

【例】do-while循环


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/eafb074f214240048e347bc1ee679174~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203165723356.png"  /></p>

【例】while循环


<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/f15cf4ecdac946c9b83e973d444a8489~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203165840320.png"  /></p>

【例】for循环：类似，略



### 3.6.8 switch语句

C语言的`switch`语句，分支数量较多时，使用**跳转表**使得比if-else效率高（节省`cmp`，空间换时间）

【例】书中示例：定义了一个数组，每个case直接跳转到标号进行处理


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/3e91ccfcf3a44464bba31440eeacf8e9~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203170450844.png"  /></p>

【编译】对应的汇编语句

<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/db3a4ab680dc4415b1e73a6174f562db~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203170823382.png"  /></p>

【声明】汇编代码中，跳转表用以下声明表示

<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/b68fed3d24104b6bba101ac38d4715a1~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203170905573.png"  /></p>



## 3.7 过程（函数调用）

### 3.7.1 运行时栈

- 操作系统每个进程有自己的**运行时栈**。栈划分为**栈帧**，存放控制和数据信息等内容。当前正在执行函数的栈总是在**栈顶**。
- 假设有两个函数`P()`和`Q()`，调用链：`main()->...->P()->Q()`，**运行时栈**情况如图3-25所示：
  - **存在三个栈帧：较早的帧（比如main）、P的栈帧、Q的栈帧**
  - **函数调用时**，程序计数器PC指向Q的起始地址、P向Q提供函数实参、Q为局部变量分配空间
  - **函数返回时**，程序计数器PC指向P调Q后一条指令地址、Q返回P一个值、Q释放局部变量存储空间
  - **P的栈帧**
    - **返回地址**：P调用Q时，会将返回地址压入占中。Q返回时，P从那个地址继续向下执行
    - **参数传递**：如果Q()形参小于等于6个参数（指针和整数），可以只用寄存器实现。如果形参超过6个，需要将参数存放到P的栈帧中。
  - **Q的栈帧**：保存寄存器的值、分配局部变量空间、为它调用的函数设置参数


<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/0544b1cf5f6b480a97a595430f617207~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221203174935163.png"  /></p>



### 3.7.2 转移控制——CALL和RET

- `call`：函数调用。等价于
  - 将函数执行完成后的下一条地址压入栈中
  - 跳转到函数的首地址执行
- `ret`：函数调用返回。等价于
  - 从栈弹出地址，赋值给程序计数器%rip
  - 跳转到这个地址继续执行

【例】书中例子，main()->multstore()->mult2()

```cpp
#include <stdio.h>

long mult2(long a, long b) {
    return a * b;
}
void multstore(long x, long y, long *dest) {
    long t = mult2(x, y);
    *dest = t;
}
int main() {
    long d;
    multstore(2, 3, &d);
    printf("2 * 3 --> %ld\n", d);
    return 0;
}
```

【反汇编】

```cpp
000000000040055d <mult2>:
  40055d:       48 89 f8        mov    %rdi,%rax
  400560:       48 0f af c6     imul   %rsi,%rax
  400564:       c3              retq                         //pop %rip，rip=40056e，继续运行

0000000000400565 <multstore>:
  400565:       53              push   %rbx
  400566:       48 89 d3        mov    %rdx,%rbx
  400569:       e8 ef ff ff ff  callq  40055d <mult2>        //push 40056e，jmp 40055d
  40056e:       48 89 03        mov    %rax,(%rbx)
  400571:       5b              pop    %rbx
  400572:       c3              retq                         //pop %rip, rip=40058b，继续执行

0000000000400573 <main>:
......
  40057c:       be 03 00 00 00  mov    $0x3,%esi
  400581:       bf 02 00 00 00  mov    $0x2,%edi
  400586:       e8 da ff ff ff  callq  400565 <multstore>    //push 40058b, jmp 400565
  40058b:       48 8b 54 24 08  mov    0x8(%rsp),%rdx
......
```



### 3.7.3 数据传送（参数传递）

- **函数参数<=6**：使用**通用寄存器**传递函数参数。注意这里的寄存器有特殊规定。

<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/99b68e82a82e4cb1bbbd8475a8bbd517~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204153248917.png"  /></p>

- **函数参数>6**：1~6使用**寄存器**传递，多于6的部分通过**栈**来传递。
- **函数返回**：通过`%rax`返回


<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/5c86f4c2ec3d4a8fad0cb4c93bebbd98~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204153639979.png"  /></p>

【注意】使用栈传参时，所有数据的大小按8字节对齐，这里a4虽然是char类型，在栈存储时占8字节空间

<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/d95a8b8938d24a4f849605f17ff57c57~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204154417432.png"  /></p>


### 3.7.4 栈上的局部存储（局部变量）

- 比较简单的函数中，局部变量可以放在寄存器中。但是有些情况，局部数据必须存放在栈中：
  - **Case 1**：寄存器不足，不够存放所有的本地数据
  - **Case 2**：局部变量使用了取地址运算符'&'，产生一个地址
  - **Case 3**：局部变量是数组或结构体

【例】书中例子

```cpp
void proc(long a1, long *a1p, int a2, int *a2p, short a3, short *a3p, char a4, char *a4p);

long call_proc() {
    long x1 = 1;
    int x2 = 2;
    short x3 = 3;
    char x4 = 4;
    proc(x1, &x1, x2, &x2, x3, &x3, x4, &x4);
    return (x1+x2)*(x3-x4);
}
```

【编译】

```cpp
call_proc:
        subq    $32, %rsp           //分配了32字节的栈帧
        movq    $1, 24(%rsp)        //x1：%rsp+24 ~ %rsp+31字节
        movl    $2, 20(%rsp)        //x2：20~23字节
        movw    $3, 18(%rsp)        //x3：18~19字节
        movb    $4, 17(%rsp)        //x4：17字节
        leaq    17(%rsp), %rax      //参数8：&x4
        movq    %rax, 8(%rsp)
        movl    $4, (%rsp)          //参数7:x4
        leaq    18(%rsp), %r9       //参数6：&x3
        movl    $3, %r8d            //参数5：x3
        leaq    20(%rsp), %rcx      //参数4：&x2
        movl    $2, %edx            //参数3：x2
        leaq    24(%rsp), %rsi      //参数2：&x1
        movl    $1, %edi            //参数1：x1
        call    proc                //call proc()
        movslq  20(%rsp), %rdx      //rdx = x2
        addq    24(%rsp), %rdx      //rdx = x1+x2
        movswl  18(%rsp), %eax      //eax = x3
        movsbl  17(%rsp), %ecx      //ecx = x4
        subl    %ecx, %eax          //eax = eax-ecx = x3-x4
        cltq                        //convert to long
        imulq   %rdx, %rax          //(x1+x2)*(x3-x4)
        addq    $32, %rsp           //释放32字节的局部变量
        ret
```

【说明】栈帧情况：这里出现Case 1和Case 2。注意：局部变量按字节对齐；传参按8字节对齐


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/8f60ef8af6ab4c78a490c8965f8bc3b7~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204163012158.png"  /></p>



### 3.7.5 寄存器中的局部存储空间

- 由于16个通用寄存器是共享的，所以函数调用时，被调用函数用到的**寄存器可能会被覆盖，所以需要压栈保存**。根据函数调用过程，寄存器分成两类：
  - **被调用者保存**：%rbx、%rbp、%r12~%r15（可能需压栈）
  - **调用者保存**：%rax、%rcx、%rdx、%rsi、%rdi、%r8~%r11（无需压栈）

【例】书中例子：这里由于P()中用到了`%rbp`和`%rbx`，所以要将环境的`%rbp`和`%rbx`压栈保存起来，最后函数返回的时候从栈恢复。注意压入顺序和弹出顺序是相反的（栈后进先出）。

<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/cef7d6333e9f4816bf97a6052263c643~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204171335111.png"  /></p>



### 3.7.6 递归过程

- **递归**：函数调用自己本身。实现和调用其他函数时一样的

【例】计算阶乘，每递归一次都会有一个栈帧

<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/3d84757be1a34e16a6b23a2531756cd5~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204173107667.png"  /></p>

【注意】如果递归次数太多不建议用递归，效率低且容易栈溢出



## 3.8 数组分配和访问

### 3.8.1 基本原则

- C语言中，定义数组的方式：$T\ A[N];$ 
  - $T$表示数组的数据类型，$N$表示数组的大小
  - 令$L = sizeof(T)$，则内存分配了$L*N$字节的**连续区域**
  - 令起始位置为$x_A$，则第$i$个数组元素存放在$x_A+L*i$处

【例】64位操作系统中，定义了以下数组，则数组相关信息如下：

```cpp
char    A[12];       //char占1字节
char   *B[8];        //指针占8字节
int    C[6];         //int占4字节
double *D[5];        //指针占8字节
```

<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/0065b754c0d9414b8d08ea38d2a8d8bc~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204175427259.png"  /></p>

【例】数组的访问：将int数组$C[i]$的值赋值到`%eax`中。则$C[i] = *(C + i) = x_C + 4i$

```cpp
//C in %rdx, i in rcx
movl (%rdx, %rcx, 4), %eax
```



### 3.8.2 指针运算

- 指针的计算与数组数据类型大小相关。以3.8.1节数组的例子为例：
  - $A+1 = x_A+1 = \&A[1]，A[1] = *(A+1) = *(x_A+1)$
  - $C+1 = x_C+4 = \&C[1]，C[1] = *(C+1) = *(x_C+4)$



### 3.8.3 嵌套数组（多维数组）

- 二维数组的定义：$T\ D[R][C]$
  - 数组元素$D[i][j]$的内存地址为：$\&D[i][j] = x_D+L(C*i+j)$
  - 在内存本质上都存储为一维数组


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/0bc0532eb3fe4f19b3d7c548991db231~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204183753344.png"  /></p>

【例】将$5\times3$的int数组$A[i][j]$的值赋值到`%eax`中。则$A[i][j]=x_A+4*(3*i+j)=x_A+12i+4j$

```cpp
//A in %rdi, i in %rsi, j in %rdx
leaq (%rsi,%rsi,2), %rax        //Compute 3*i
leaq (%rdi,%rax,4), %rax        //Compute x_a+12i
movl (%rax,%rdx,4), %eax        //Compute x_a+12i+4j
```



### 3.8.4 定长数组 & 3.8.5 变长数组

书中这两节的意思，其实是想说编译器在解析多维数组时，会尽可能地优化它。比如：GCC可以直接识别出多维数组的元素步长，生成的汇编指令避免直接计算$\&D[i][j] = x_D+L(C*i+j)$，能显著提高程序性能。

【例】矩阵乘法例子，编译器有不少优化：
- 在第2行：取左移计算\*64；
- 在第4行：使用`leaq`计算元素地址；
- 在第11行：+4运算取A数组的下一个行元素；
- 在第12行：+64运算取B数组的下一个列元素等等。

<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/cbab584db83d4ed4822fdb00bdc2c499~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204184703063.png"  /></p>



## 3.9 结构体和联合

### 3.9.1 结构体struct

- C语言中使用struct声明结构体：

```cpp
struct rec {
    int i;        //4字节
    int j;        //4字节
    int a[2];     //共8字节
    int *p;       //8字节
};
struct rec* r;
```


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/dc6953a111804a65a53f16d534456fb4~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221204235612853.png"  /></p>

【例】结构体的访问，与数组访问类似，首地址加上对应偏移量`r->j = r->i;`

```cpp
//r in %rdi
movl (%rdi), %eax        //eax = r->i
movl %eax, 4(%rdi)       //r->j = eax
```

【例】取`&(r->a[i])`的值

```cpp
//r in %rdi, i in %rsi
leaq 8(%rdi, %rsi, 4), %rax    //rax = r->a + 4i = rdi + 8 + rsi*4
```

【例】`r->p = &r->a[r->i + r->j];`

```cpp
//r in %rdi
movl 4(%rdi), %eax          //eax = r->j
addl (%rdi), %eax           //eax = r->i + r->j
cltq                        //eax extend to rax
leaq 8(%rdi,%rax,4), %rax   //rax = &r->a[r->i + r->j]
movq %rax, 16(%rdi)         //r->p = rax
```


### 3.9.3 结构体对齐

- 结构体的数据有对齐限制，原则：**任何K字节的基本对象的地址必须是K的倍数**


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/96bc0ef4c365493196e853bbcb5fd127~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221205001432668.png"  /></p>

【例】结构体S1，对齐后char会插入一个3字节的空隙

```cpp
struct S1 {
    int i;
    char c;
    int j;
};
```


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/672f37ee9e6949128e20d8a8946843fc~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221205001711268.png"  /></p>

【例】结构体S2，对齐后char会插入一个3字节的空隙

```cpp
struct S2 {
    int i;
    int j;
    char c;
};
```


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/3a24489dc5754dcd8ff8040f1f76ded8~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221205001823852.png"  /></p>

【例】结构体，计算总结构体一共有56字节。这里如果按大小顺序降序排列结构元素，可以节省结构体大小

```cpp
struct {
    char *a;
    short b;
    double c;
    char d;
    float e;
    char f;
    long g;
    int h;
};
```


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/ed3f6a96352d4ae08db19fdafe769801~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221205002307251.png"  /></p>

【注】更多数据对齐的情况，还是要根据不同型号处理器和编译系统进行具体分析。


### 3.9.2 联合

- **联合union**：所有元素共享同一存储区域。也就是说，**联合体的大小取决于最大元素的大小**
- **使用场景**：已知两个不同字段是互斥的，可以声明为联合，减小分配空间的总量 *（内核代码常用）*

【例】联合体U3，大小是8字节。如果是结构体则占24字节。

```cpp
union U3 {
    char c;        //1字节
    int i[2];      //共8字节
    double v;      //8字节
};
```

【例】书中例子：二叉树数据结构

```cpp
//使用结构体，每个节点需要32字节
struct node_s {
    struct node_s *left;    //左节点，内部节点
    struct node_s *right;   //右节点，内部节点
    double data[2];         //叶子节点
};

//使用联合体，总计24字节（4字节type + 4字节padding + 16字节struct）
type enum { N_LEAF, N_INTERNAL } nodetype_t;
struct node_u {
    nodetype_t type;    //由于引入联合后无法区分是内部节点还是叶子节点，增加type类型，占4字节
    union {
        struct {        //内部节点
            struct node_u *left;
            struct node_u *right;
        } internal;
        double data[2]; //叶子节点
    } info;
};
```

【注意】联合体将不同数据类型结合在一起，有字节序问题（大端机器和小端机器表现不同）

```cpp
include <stdio.h>

int main() {
    union {
        long l;
        int i[2];
    } temp;
    temp.i[0] = 0x01234567;
    temp.i[1] = 0x89abcdef;
    printf("0x%lx\n", temp.l);
    return 0;
}
```

【输出】在小端机器上输出：0x89abcdef01234567


## 3.10 数据和控制结合

### 3.10.3 内存越界引用和缓存区溢出

**缓冲区溢出**（buffer overflow）：C对于数组引用不进行任何边界检查，所以局部的数组如果溢出会**破坏栈**，造成严重错误

```cpp
#include <stdio.h>

int main() {
    char buf[8];
    gets(buf);
    puts(buf);
    return 0;
}
```

【测试】输入abcdefghijklmn，出现栈溢出

```cpp
abcdefghijklmn
abcdefghijklmn
*** stack smashing detected ***: ./main terminated
Aborted (core dumped)
```



### 3.10.4 对抗缓冲区溢出方法

病毒可能利用缓冲区溢出的特性破坏计算机系统。有几种对抗方法：

- **栈随机化**：每次执行程序，栈的位置都不固定

```cpp
#include <stdio.h>

int main()
{
    long local;
    printf("local at %p\n", &local);
    return 0;
}
```

【测试】

```shell
root@ubuntu:~/test# ./main
local at 0x7ffd5d767218
root@ubuntu:~/test# ./main
local at 0x7ffea89123a8
```

- **栈破坏检测**：在栈帧中任何局部缓冲区与栈状态之间，存储一个特殊的**金丝雀值（canary）**。当检测到值被改变，表示越界，程序异常终止（可以使用`-fno-stack-protector`阻止GCC产生这种代码）


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/a61643682de24f009f3e69be705c0854~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221205215038012.png"  /></p>

- **限制可执行代码区域**：栈标记为是否可读写、可执行，消除攻击者向系统中插入可执行代码的能力。



### 3.10.5 变长栈帧

- 编译器**通常能够提前确定**一个函数要提前为栈帧分配多少空间。但有些函数的**局部存储是变长的**

【例】栈帧申请8n字节的变长数组，编译器无法确认n的值。返回时要释放栈帧

```cpp
long vframe(long n, long idx, long *q) {
    long i;
    long *p[n];
    p[0] = &i;
    for (i = 1; i < n; i++) {
        p[i] = q;
    }
    return *p[idx];
}
```

【编译】

```cpp
//long vframe(long n, long idx, long *q)
//n in %rdi, idx in %rsi, q in %rdx
vframe:
    pushq   %rbp
    movq    %rsp, %rbp            //栈指针记录在%rbp处
    subq    $16, %rsp             //分配16字节空间存放long i; 需要16字节对齐
    leaq    22(,%rdi,8), %rax     //rax = 8n+22
    andq    $-16, %rax            //rax = (8n+22) & 1111 0000，需要16字节对齐
    subq    %rax, %rsp            //分配long *p[n]，总共分配了rax字节空间
......
    leave                         //恢复rsp和rbp, 等价于movq %rbp, %rsp; popq %rbp
    ret
```

【说明】栈帧情况：使用`subq %rax, %rsp`的方式申请8n的内存大小，使用`leave`恢复初始的`%rsp`


<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/16b541e063b24630abd071c6a07cb83e~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206002542637.png"  /></p>



## 3.11 浮点代码

### 3.11.1 浮点传送和转换操作

- 为支持图形和图像处理，引入media寄存器集（MMX、SSE、AVX等），支持浮点的数学运算。这些指令允许多个操作以并行模式运行。本书基于**AVX2版本**
- GCC编译浮点程序，给定命令行参数`-mavx2`时，会生成AVX2代码
- 浮点处理时，允许数据存放在16个**YMM寄存器**中，名字是`%ymm0`\~`%ymm15`。每个YMM寄存器是256位（32字节），低128位（16字节）对应**XMM寄存器** `%xmm0`\~`%xmm15`


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/aeae0b10eba34d4e8747d3cf76557f80~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206072654598.png"  /></p>

### 3.11.1 浮点传送和转换操作

- **VMOV**：把数据从一个XMM寄存器复制到另一个XMM寄存器，或者从内存复制。类似于整数的MOV


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/57d47d0887df4b6f8136c5c443b9b6d1~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206073718618.png"  /></p>

【例】

```cpp
float float_mov(float v1, float *src, float *dst) {
    float v2 = *src;
    *dst = v1;
    return v2;
}
```

【编译】

```cpp
//float float_mov(float v1, float *src, float *dst)
//v1 in %xmm0, src in %rdi, dst in %rsi
float_mov:
        vmovaps %xmm0, %xmm1     //%xmm1 = v1
        vmovss  (%rdi), %xmm0    //%xmm0 = *src
        vmovss  %xmm1, (%rsi)    //*dst = v1
        ret                      //return %xmm0
```



- **浮点数和整数之间转换**——`VCVT`
  - 浮点数$\to$整数：指令会执行截断，向零舍入
    - `vcvttsd2siq (%rdx), %r8`：从内存读取浮点数，转为长整型
  - 整数$\to$浮点数：三元操作，第二个源通常可以忽略
    - `vcvtsi2sdq %rax, %xmm1, %xmm1`：从`%rax`读取一个长整数，转为double，存进`%xmm1`的低字节中
  - 单精度浮点$\to$双精度浮点：把XMM寄存器的两个低位单精度值扩展为两个双精度值 *（不必细扣）*
    - `vunpcklps %xmm0, %xmm0, %xmm0`
    - `vcvtps2pd %xmm0, %xmm0`
  - 双精度浮点$\to$单精度浮点：把XMM寄存器的两个双精度值扩展为两个低位单精度值 *（不必细扣）*
    - `vmovddup %xmm0, %xmm0`
    - `vcvtpd2psx %xmm0, %xmm0`


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/63c0845905704e79888c194aec8979d1~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206074757302.png"  /></p>

【例】书中例子


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/6d738874b2aa47db97685b082c089a11~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206081438709.png"  /></p>

【编译】


<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/59dca61fc0db4e8797684ffb46d47fcf~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206081506962.png"  /></p>



### 3.11.2 函数中的浮点代码

- **函数参数**：
  - **整数和指针**使用**通用寄存器**传递
  - **浮点数**使用**XMM寄存器**传递：`%xmm0`\~`%xmm7`传递最多8个浮点参数
- 函数返回：使用`%xmm0`返回浮点值

【注】所有的XMM寄存器都是调用者保存的，被调用者可以随意覆盖

### 3.11.3 浮点运算
- **AVX2浮点指令**：区分单精度和双精度。
    - 指令含1个或2个源操作数，一个目的操作数。
    - 第一个源操作数可以是XMM寄存器或内存位置
    - 第二个源操作数和目的操作数都必须是XMM寄存器

<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/02f1be361c494779b9ff6fd519b6cfe8~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206233301192.png"  /></p>

【例】
```cpp
double funct(double a, float x, double b, int i) {
    return a*x - b/i;
}
```
【编译】
```cpp
//double funct(double a, float x, double b, int i)
//a in %xmm0, x in %xmm1, b in %xmm2, i in %edi, return %xmm0
funct:
        vunpcklps       %xmm1, %xmm1, %xmm1    //The following tow instructions convert x to double
        vcvtps2pd       %xmm1, %xmm1
        vmulsd  %xmm0, %xmm1, %xmm0            //Multiply a by x
        vcvtsi2sd       %edi, %xmm1, %xmm1     //Convert i to double
        vdivsd  %xmm1, %xmm2, %xmm2            //Compute b/i
        vsubsd  %xmm2, %xmm0, %xmm0            //Substract from a*x
        ret

```

### 3.11.4 浮点常量定义
【例】书中例子
```cpp
double cel2fahr(double temp) {
    return 1.8 * temp + 32.0;
}
```
【编译】
```cpp
cel2fahr:
        vmulsd  .LC0(%rip), %xmm0, %xmm0
        vaddsd  .LC1(%rip), %xmm0, %xmm0
        ret
.LC0:
        .long   3435973837        //数字1.8的低4字节
        .long   1073532108        //数字1.8的高4字节
.LC1:
        .long   0                 //数字32.0的低4字节
        .long   1077936128        //数字32.0的高4字节
```
【说明】.LC0表示1.8的常量，含两个值，3435973837(0xcccccccd)和1073532108(0x3ffccccc)

- 按小端法字节排序，常数编码为：0x3ffccccc cccccccd
- 根据第2章分析，$V=(-1)^s \times M \times 2^E$，阶码位k=10，偏移Bias=2^k-1=1023，小数位n=52
  - 符号$s=0$，表示正数
  - 指数字段$exp=0x3ff=1023$，减去偏移$E=1023-1023=0$
  - 小数字段$M=0xccccc\ cccccccd$，二进制是0.8，规格化后值为1.8

$$
\frac{0xccccc\ cccccccd}{2^{52} }=0.80000000000000004440...
$$

- 所以小数$V=(-1)^0*1.8*2^0=1.8$

### 3.11.5 浮点代码的位级操作
- 位级异或(XOR)、位级与(AND)

<p align=center><img src="https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/ef0d9749459748fa932895dd35924014~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206234539858.png"  /></p>

### 3.11.6 浮点比较
- 比较单精度(ucomiss)、比较双精度(ucomisd)，与正数的`cmpq`效果一样


<p align=center><img src="https://p6-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/37617ad9fc4946a19687f0eab9c14f31~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206235020091.png"  /></p>

- 浮点数比较后，会设置三个条件码：`ZF`(零标志位)、`CF`(进位标志位)、`PF`(奇偶标志位)
  - 注：这里比整数运算多一个PF位，目的是区分任一操作数为$NaN$的情况


<p align=center><img src="https://p1-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/cba2bd32d92b49a09e85ae3bf4320573~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206235355028.png"  /></p>

【例】


<p align=center><img src="https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/e059f48e5e28434f98e7ffb2452adc65~tplv-k3u1fbpfcp-watermark.image?" alt="image-20221206235929893.png"  /></p>



### 3.11.7 总结

- **相似**：AVX2指令的浮点数操作 类似于 整数的操作
- **区别**：AVX2指令的规则会更复杂，且支持并行操作，使计算执行得更快